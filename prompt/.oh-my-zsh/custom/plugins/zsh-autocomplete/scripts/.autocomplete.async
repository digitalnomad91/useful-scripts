#!/bin/zsh
zmodload -F zsh/zpty b:zpty
zmodload -F zsh/parameter p:funcstack p:functions p:parameters
zmodload -F zsh/system b:sysopen p:sysparams
zmodload -F zsh/zselect b:zselect
zmodload -F zsh/terminfo b:echoti p:terminfo
zmodload -F zsh/zutil b:zparseopts
builtin autoload -RUz \
    add-zle-hook-widget \
    is-at-least

typeset -gi _autocomplete__buffer_start_line=1
typeset -g ZSH_AUTOSUGGEST_USE_ASYNC=yes
local -Pa log_functions=()

builtin zle -N history-incremental-search-backward .autocomplete.async.history-incremental-search

.autocomplete.async.precmd() {
  [[ -v ZSH_AUTOSUGGEST_IGNORE_WIDGETS ]] &&
      ZSH_AUTOSUGGEST_IGNORE_WIDGETS+=(
          history-incremental-search-backward
      )

  local -PF delay=
  builtin zstyle -s :autocomplete: min-delay delay
  (( delay += 0.1 ))
  typeset -gF _autocomplete__async_avg_duration=$delay

  # Start names with `.` to avoid getting wrapped by syntax highlighting.
  builtin zle -N .autocomplete.async.pty.zle-widget
  builtin zle -C .autocomplete.async.pty.completion-widget list-choices \
      .autocomplete.async.pty.completion-widget
  builtin zle -N .autocomplete.async.complete.fd-widget
  builtin zle -C ._list_choices list-choices .autocomplete.async.list-choices.completion-widget

  add-zle-hook-widget line-init .autocomplete.async.read-cursor-position
  add-zle-hook-widget line-init .autocomplete.async.reset-context
  add-zle-hook-widget line-init .autocomplete.async.complete

  add-zle-hook-widget line-pre-redraw .autocomplete.async.complete
  add-zle-hook-widget line-finish .autocomplete.async.clear
  add-zle-hook-widget isearch-update .autocomplete.async.isearch-update
  add-zle-hook-widget isearch-exit .autocomplete.async.isearch-exit

  add-zsh-hook zshexit .autocomplete.async.stop
}

.autocomplete.async.read-cursor-position() {
  emulate -L zsh
  # Cannot set strict shell options here, because other programs' async
  # callbacks can interrupt the code and then get executed in the same context.
  # See https://github.com/marlonrichert/zsh-autocomplete/issues/342

  (( KEYS_QUEUED_COUNT || PENDING )) &&
      return

  # $MC_SID is set when we're in a subshell started by Midnight Commander.
  # See https://github.com/marlonrichert/zsh-autocomplete/issues/269
  if [[ -v MC_SID || ! ( -v terminfo[u6] && -v terminfo[u7] ) ]]; then
    local -i max_lines=
    builtin zstyle -s ":autocomplete:list-choices::::" list-lines max_lines ||
        (( max_lines = LINES / 2 ))
    typeset -gi _autocomplete__buffer_start_line=$(( min( max( LINES - max_lines, 1 ), LINES ) ))
    return 0
  fi

  # Cursor position report (u6) always contains two %d markers.
  local -Pa CPR=( "${(s:%d:)$( echoti u6 )}" )

  # If there is a %i before the first %d, then decrement both %d values by 1.
  local -Pi i=${${${(M)CPR[1]%'%i'}:+1}:-0}
  CPR[1]=${CPR[1]%'%i'}

  local REPLY=
  local -P Y=
  echoti u7  # Send cursor position request.

  # Parse the cursor position report.
  read -rsk $#CPR[1]  # Discard the preamble.
  while [[ $REPLY != $CPR[2] ]]; do
    read -rsk
    Y+=$REPLY
  done
  Y="${Y%$CPR[2]}"  # Discard the separator.
  Y=${(M)Y%%<->}    # Keep trailing digits only (to work around input pollution).

  # Flush the rest.
  while [[ $REPLY != $CPR[3] ]]; do
    read -rsk
  done

  typeset -gi _autocomplete__buffer_start_line=$(( min( max( Y - i, 1 ), LINES ) ))
}

.autocomplete.async.history-incremental-search() {
  if [[ $curcontext == $WIDGET* ]]; then
    unset curcontext
  else
    typeset -g curcontext=${WIDGET}:::
  fi
  .autocomplete.async.start
}

.autocomplete.async.reset-context() {
  local context
  builtin zstyle -s :autocomplete: default-context context
  typeset -g curcontext=$context
  return 0
}

.autocomplete.async.isearch-update() {
  typeset -gi _autocomplete__isearch=1
}

.autocomplete.async.isearch-exit() {
  .autocomplete.zle-flags $LASTWIDGET
  unset _autocomplete__isearch
}

.autocomplete.async.complete() {
  .autocomplete.zle-flags $LASTWIDGET

  (( KEYS_QUEUED_COUNT || PENDING )) &&
      return

  # #549: Work around bug in zdharma/fast-syntax-highlighting.
  [[ -v _FAST_MAIN_CACHE ]] &&
      _zsh_highlight

  typeset -g _autocomplete__region_highlight=( "$region_highlight[@]" )

  if [[ -v ZSH_AUTOSUGGEST_IGNORE_WIDGETS ]] && (( ZSH_AUTOSUGGEST_IGNORE_WIDGETS[(I)$LASTWIDGET] )); then
    unset POSTDISPLAY
    _zsh_autosuggest_fetch
  fi

  # This means we're in the completion menu.
  [[ $LASTWIDGET == .autocomplete.async.complete.fd-widget ]] &&
      return

  .autocomplete.async.stop

  if (( REGION_ACTIVE )) ||
      [[ -v _autocomplete__isearch && $LASTWIDGET == *(incremental|isearch)* ]]; then
    builtin zle -Rc
    return 0
  fi

  [[ $LASTWIDGET == (_complete_help|(|.)(describe-key-briefly|what-cursor-position|where-is)) ]] &&
      return

  [[ $KEYS == ([\ -+*]|$'\e\t') ]] &&
      builtin zle -Rc

  if [[ $_lastcomp[insert] == *unambiguous ]]; then
    local -a nolast=()
    is-at-least 5.9 &&
        nolast=( -f nolast )
    builtin zle .auto-suffix-retain $nolast  # Make the cursor stay in the right place.
  fi

  .autocomplete.async.start
  return 0
}

.autocomplete.async.clear() {
  builtin zle -Rc
  unset _autocomplete__isearch
  .autocomplete.async.stop
  .autocomplete.async.reset-context
  return 0
}

.autocomplete.async.stop() {
  local fd=$_autocomplete__async_complete_fd
  unset _autocomplete__async_complete_fd
  unset _autocomplete__mesg _autocomplete__comp_mesg _autocomplete__words _autocomplete__current
  if [[ $fd == <-> ]]; then
    builtin zle -F $fd 2> /dev/null
    exec {fd}<&-
  fi
}

.autocomplete.async.start() {
  local fd=
  sysopen -r -o cloexec -u fd <(
    typeset -F SECONDS=0
    setopt promptsubst
    PS4=$_autocomplete__ps4
    .autocomplete.async.start.inner
  )
  builtin zle -Fw "$fd" .autocomplete.async.complete.fd-widget
  typeset -g _autocomplete__async_complete_fd=$fd

  # There's a weird bug in Zsh < 5.8, where ^C stops working unless we force a fork.
  # See https://github.com/zsh-users/zsh-autosuggestions/issues/364
  command true
}

.autocomplete.async.start.inner() {
  {
    local -F min_delay=
    builtin zstyle -s :autocomplete: min-delay min_delay ||
        min_delay=0.05

    # Directly using $(( [#10] 100 * max( 0, min_delay - SECONDS ) )) leads to 0
    # in Zsh >= 5.9, as the result of the max call is converted to an integer.
    # See https://github.com/marlonrichert/zsh-autocomplete/issues/441
    local -i timeout=$(( 100 * max( 0, min_delay - SECONDS ) ))
    zselect -t $timeout

    local -P hooks=( chpwd periodic precmd preexec zshaddhistory zshexit )
    builtin unset ${^hooks}_functions &> /dev/null
    $hooks[@] () { : }

    local -P hook=
    for hook in \
        zle-{isearch-{exit,update},line-{pre-redraw,init,finish},history-line-set,keymap-select}
    do
      builtin zle -N $hook .autocomplete.async.pty.no-op
    done
    {
      local REPLY=
      zpty AUTOCOMPLETE .autocomplete.async.pty
      local -Pi fd=$REPLY

      zpty -w AUTOCOMPLETE $'\t'

      local header=
      zpty -r AUTOCOMPLETE header $'*\C-B'

      local -a reply=()
      local text=

      # Directly using $(( [#10] 100 * max( 0, 100 * _autocomplete__async_avg_duration - SECONDS ) ) ))
      # leads to 0 in Zsh >= 5.9, as the result of the max call is converted to an integer.
      # See https://github.com/marlonrichert/zsh-autocomplete/issues/441
      local -i timeout=$(( 100 * max( 0, 100 * _autocomplete__async_avg_duration - SECONDS ) ))
      zselect -rt $timeout "$fd" &&
          zpty -r AUTOCOMPLETE text $'*\C-C'
    } always {
      zpty -d AUTOCOMPLETE
    }
  } always {
    # Always produce output, so we always reach the callback, so we can close the fd and unset
    # $_autocomplete__async_complete_fd (if necessary).
    print -rNC1 -- "$SECONDS" "${text%$'\0\C-C'}"
  }
} 2>>| $_autocomplete__log_async
log_functions+=( .autocomplete.async.start.inner )

.autocomplete.async.pty() {
  typeset -g _autocomplete__lbuffer="$LBUFFER" _autocomplete__rbuffer="$RBUFFER"

  builtin bindkey $'\t' .autocomplete.async.pty.zle-widget
  local __tmp__=
  builtin vared __tmp__
} 2>>| $_autocomplete__log_pty
log_functions+=( .autocomplete.async.pty )

.autocomplete.async.pty.no-op() {
  :
}

.autocomplete.async.pty.zle-widget() {
  .autocomplete.async.pty.zle-widget.inner "$@"
}

.autocomplete.async.pty.zle-widget.inner() {
  # The completion widget sometimes returns without calling its function. So, we need to print all
  # our control characters here, to ensure we don't end up waiting endlessly to read them.
  {
    print -n -- '\C-B'
    LBUFFER=$_autocomplete__lbuffer
    RBUFFER=$_autocomplete__rbuffer

    setopt $_autocomplete__comp_opts[@]
    [[ -n $curcontext ]] &&
        setopt $_autocomplete__ctxt_opts[@]
    builtin zle .autocomplete.async.pty.completion-widget -w 2> /dev/null
  } always {
    print -rNC1 -- \
        "$_autocomplete__list_lines" "$_autocomplete__mesg" "$_autocomplete__comp_mesg[@]" $'\C-C'
    builtin kill $sysparams[pid]
  }
} 2>>| $_autocomplete__log_pty
log_functions+=( .autocomplete.async.pty.zle-widget.inner )

.autocomplete.async.pty.completion-widget() {
  .autocomplete.async.pty.completion-widget.inner "$@"
}

.autocomplete.async.pty.completion-widget.inner() {
  if ! .autocomplete.async.sufficient-input; then
    typeset -gi _autocomplete__list_lines=0
    return
  fi
  if .autocomplete.async.same-state; then
    typeset -gi _autocomplete__list_lines=$_lastcomp[list_lines]
    return
  fi

  unset _autocomplete__mesg _autocomplete__comp_mesg
  {
    unset 'compstate[vared]'

    .autocomplete.patch _message
    _message() {
      compadd() {
        typeset -ga _autocomplete__comp_mesg=( "$@" )
        builtin compadd "$@"
      }
      zformat() {
        builtin zformat "$@"
        typeset -ga _autocomplete__comp_mesg=( "$gopt[@]" -x "$format" )
      }
      autocomplete:_message:old "$@"
      unfunction zformat
      functions[compadd]="$functions[.autocomplete.compadd]"
    }

    local +h -a comppostfuncs=( .autocomplete.async.pty.message )

    if [[ $curcontext == *history-* ]]; then
      autocomplete:_main_complete:new - history-lines _autocomplete.history_lines
    else
      local curcontext=list-choices:::
      .autocomplete.async.list-choices.main-complete
    fi
  } always {
    typeset -gi _autocomplete__list_lines=$compstate[list_lines]
  }
} 2>>| $_autocomplete__log_pty
log_functions+=( .autocomplete.async.pty.completion-widget.inner )

.autocomplete.async.pty.message() {
  typeset -g _autocomplete__mesg=$mesg
  return 0
}

.autocomplete.async.complete.fd-widget() {
  setopt promptsubst
  local +h PS4=$_autocomplete__ps4
  .autocomplete.async.complete.fd-widget.inner "$@"
}

.autocomplete.async.complete.fd-widget.inner() {
  local -i fd=$1
  {
    builtin zle -F $fd # Unhook ourselves immediately, so we don't get called more than once.

    # Ensure our input will not be stopped.
    unset _autocomplete__async_complete_fd

    .autocomplete.zle-flags ||
        return 0

    local -a reply=()
    IFS=$'\0' read -rAu $fd
    shift -p reply
  } always {
    exec {fd}<&-
  }

  unset _autocomplete__mesg _autocomplete__comp_mesg

  # If a widget can't be called, zle always returns true.
  # Thus, we return false on purpose, so we can check if our widget got called.
  setopt $_autocomplete__comp_opts[@]
  [[ -n $curcontext ]] &&
      setopt $_autocomplete__ctxt_opts[@]
  if ! builtin zle ._list_choices -w "$reply[@]" 2>>| $_autocomplete__log; then

    typeset -g region_highlight=( "$_autocomplete__region_highlight[@]" )

    # Need to call this here, because on line-pre-redraw, $POSTDISPLAY is empty.
    [[ -v functions[_zsh_autosuggest_highlight_apply] ]] &&
        _zsh_autosuggest_highlight_apply

    # Refresh if and only if our widget got called. Otherwise, Zsh will crash (eventually).
    builtin zle -R
  else
    .autocomplete.async.stop
  fi
  return 0
} 2>>| $_autocomplete__log
log_functions+=( .autocomplete.async.complete.fd-widget.inner )

.autocomplete.async.sufficient-input() {
  local min_input=
  if ! builtin zstyle -s ":autocomplete:${curcontext}:" min-input min_input; then
    if [[ $curcontext == *history-* ]]; then
      min_input=0
    else
      min_input=1
    fi
  fi

  local ignored=
  builtin zstyle -s ":autocomplete:${curcontext}:" ignored-input ignored

  if (( ${#words[@]} == 1 && ${#words[CURRENT]} < min_input )) ||
      [[ -n $ignored && $words[CURRENT] == $~ignored ]]; then
    compstate[list]=
    false
  else
    true
  fi
}

.autocomplete.async.same-state() {
  [[ $_autocomplete__words == $words && $_autocomplete__current == $CURRENT ]]
}

.autocomplete.async.list-choices.completion-widget() {
  unset _autocomplete__mesg _autocomplete__comp_mesg _autocomplete__words _autocomplete__current

  if [[ $1 != <->.<-> || $2 != <-> ]]; then
    compstate[list]=
    return
  fi

  .autocomplete.async.sufficient-input ||
      return 2

  local -PF _seconds_=$1
  local -Pi _list_lines_=$2
  local -P _mesg_=$3
  shift 3

  if [[ -n $compstate[old_list] ]] && .autocomplete.async.same-state; then
    compstate[old_list]=keep
  elif [[ $_list_lines_ == 1 && -n $1 ]]; then
    builtin compadd "$@"
  elif [[ $_list_lines_ == 1 && -n $_mesg_ ]]; then
    builtin compadd -x "$_mesg_"
  else
    typeset -gF _autocomplete__async_avg_duration=$((
        .1 * _seconds_ + .9 * _autocomplete__async_avg_duration
    ))
    if [[ $curcontext == *history-* ]]; then
      autocomplete:_main_complete:new - history-lines _autocomplete.history_lines
    else
      local curcontext=list-choices:::
      .autocomplete.async.list-choices.main-complete
    fi
  fi

  typeset -ga _autocomplete__comp_mesg=( "$@" )
  typeset -ga _autocomplete__words=( "$words[@]" )
  typeset -gi _autocomplete__current=$CURRENT
  typeset -g _autocomplete__mesg=$_mesg_

  # Workaround: In Zsh <= 5.9.0, comppostfuncs don't get called after completing subscripts.
  unset MENUSELECT MENUMODE
  compstate[insert]=
  _lastcomp[insert]=
  if [[ -v _autocomplete__partial_list ]]; then
    builtin compadd -J -last- -x '%F{black}%K{12}(MORE)%f%k'
    _lastcomp[list_lines]=$compstate[list_lines]
  fi

  return 2  # Don't return 1, to prevent beeping.
} 2>>| $_autocomplete__log
log_functions+=( .autocomplete.async.list-choices.completion-widget )

.autocomplete.async.list-choices.main-complete() {
  {
    local -i max_lines=
    builtin zstyle -s ":autocomplete:${curcontext}:" list-lines max_lines ||
        (( max_lines = LINES / 2 ))
    max_lines=$(( min( LINES - ( 1 + BUFFERLINES ), max_lines ) ))
    local -Pi lines_below_buffer=$(( LINES - ( _autocomplete__buffer_start_line + BUFFERLINES ) ))
    local -i _autocomplete__max_lines=$(( max( max_lines, lines_below_buffer ) ))
    () {
      emulate -L zsh
      setopt $_autocomplete__func_opts[@]

      [[ -v functions[compadd] ]] &&
          functions[autocomplete:async:compadd:old]="$functions[compadd]"
      functions[compadd]="$functions[.autocomplete.async.compadd]"
      # functions -T compadd .autocomplete.compadd #_describe
    } "$@"
    autocomplete:_main_complete:new "$@"
  } always {
    unfunction compadd comptags 2> /dev/null
    if [[ -v functions[autocomplete:async:compadd:old] ]]; then
      functions[compadd]="$functions[autocomplete:async:compadd:old]"
      unfunction autocomplete:async:compadd:old
    fi
  }
}
# functions -T .autocomplete.async.list-choices.main-complete

.autocomplete.async.compadd() {
  local -Pi _ret_=1

  local -A _opts_=()
  local -a _displ_=() _dopt_=() _groupname_=()
  zparseopts -A _opts_ -E -- D: O: X: x: d+:=_dopt_ l

  if [[ -v _opts_[-x] && $# == 2 ]]; then
    .autocomplete.compadd "$@"
    return
  fi

  local -Pi _avail_list_lines_=$((
      max(
          0,
          _autocomplete__max_lines - 1 - ${${_opts_[(i)-[Xx]]}:+1} - compstate[list_lines]
      )
  ))

  if (( funcstack[(I)_describe] )); then

    if [[ -z $_opts_[-D] ]]; then
        .autocomplete.compadd "$@"

        return  #!

    else
      _displ_name_=$_opts_[-D]

      if ! [[ -v _autocomplete__partial_list ]]; then
        .autocomplete.compadd "$@"
        _ret_=$?

        _displ_=( ${(@PA)_displ_name_} )

        if [[ -n $_displ_ ]]; then
            local -PaU _uniques_=( ${_displ_[@]#*:} )

          _autocomplete__max_lines=$(( _avail_list_lines_ - $#_uniques_[@] ))
          if (( _autocomplete__max_lines <= 0 )); then
            set -A $_displ_name_ "${(@)${(@o)_displ_}[1,_avail_list_lines_]}"

            .autocomplete.async.compadd.disable
          fi
        fi
      else
        set -A $_displ_name_
        _ret_=1
      fi

      return _ret_  #!
    fi
  fi

  if [[ -n $_opts_[-D]$_opts_[-O] ]]; then
    .autocomplete.compadd "$@"
    return
  fi

  [[ -v _autocomplete__partial_list ]] &&
      return 1

  local -Pi _resulting_list_lines="$(
              .autocomplete.compadd "$@"
              print -nr -- $compstate[list_lines]
          )"
  local -Pi _new_list_lines_=$(( _resulting_list_lines - $compstate[list_lines] ))
  local -Pi _remaining_list_lines=$(( _avail_list_lines_ - _new_list_lines_ ))

  (( _remaining_list_lines <= 0 )) &&
      .autocomplete.async.compadd.disable

  if (( _remaining_list_lines >= 0 )); then
    .autocomplete.compadd "$@"
    return
  fi

  typeset -p1 _dopt_ >&2

  local -a _matches_=()
  local -P _displ_name_=$_dopt_[2]
  [[ -n $_displ_name_ ]] &&
      local -a _Dopt_=( -D $_displ_name_ )

  # Collect all matching completions and remove the display strings of those that don't match.
  .autocomplete.compadd -O _matches_ $_Dopt_ "$@"

  if [[ -z $_displ_name_ ]]; then
    _displ_=( "$_matches_[@]" )
    _dopt_=( -d _displ_ )
    _displ_name_=_displ_
  fi

  local -Pi _nmatches_per_line_=$(( 1.0 * $#_matches_ / _new_list_lines_ ))

  if (( _nmatches_per_line_ < 1 )); then
    # If we need more than one line per match, then make each match fit on one line.
    _nmatches_per_line_=1

    _dopt_=( -l "$_dopt_[@]" )

    # WORKAROUND: Display strings that are exactly $COLUMNS wide are mistakenly
    # counted as spanning two lines, causing the dreaded
    # `zsh: do you wish to see all XX possibilities (YY lines)?`
    set -A $_displ_name_ ${(@r:COLUMNS-1:)${(PA)_displ_name_}[@]//$'\n'/\n}
  fi

  # Need to round this down _before_ subtracting or it will be effectively rounded up.
  local -Pi _nmatches_that_fit_=$(( _avail_list_lines_ * _nmatches_per_line_ ))

  local -Pi _nmatches_to_remove_=$(( $#_matches_ - _nmatches_that_fit_ ))

  (( _nmatches_to_remove_ > 0 )) &&
      shift -p $_nmatches_to_remove_ _matches_ $_displ_name_

  _autocomplete.compadd_opts_len "$@"
  .autocomplete.compadd $_dopt_ -a "$@[1,?]" _matches_

} 2>>| $_autocomplete__log
log_functions+=( .autocomplete.async.compadd )

.autocomplete.async.compadd.disable() {
  # Disable all further completions.
  typeset -g _autocomplete__partial_list=$curtag
  comptags() { false }
}

typeset -ga _autocomplete__log_functions=( $log_functions[@] )
